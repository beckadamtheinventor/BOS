;@DOES convert a number to a string
;@INPUT HL = number to convert
;@OUTPUT DE = string
;@OUTPUT string stored at gfx_string_temp
util_NumToString:
sys_HLToString:
	ld	de,gfx_string_temp
	push	de
	call	.entry
	xor	a,a
	ld	(de),a
	pop	de
.skip0loop:
	ld a,(de)
	cp a,'0'
	ret nz
	inc de
	jq .skip0loop
.entry:
	ld	bc,-10000000
	call	.aqu
	ld	bc,-1000000
	call	.aqu
	ld	bc,-100000
	call	.aqu
	ld	bc,-10000
	call	.aqu
	ld	bc,-1000
	call	.aqu
	ld	bc,-100
	call	.aqu
	ld	c,-10
	call	.aqu
	ld	c,b
.aqu:
	ld	a,'0' - 1
.under:
	inc	a
	add	hl,bc
	jr	c,.under
	sbc	hl,bc
	ld	(de),a
	inc	de
	ret
